file = {SOI ~ statement+ ~ EOI}
WHITESPACE = _{ " "| "\t" | NEWLINE }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }


keyword_let = {^"let"}
keyword_rule = {^"rul"}
keyword_end = {^"end"}
unary_op = {^"sub" | ^"abs" | ^"sig"}
binary_op = {^"add" | ^"sub" | ^"mul" | ^"div"| ^"and"| ^"or"| ^"eq"| ^"neq"| ^"lt"| ^"gt"| ^"leq"| ^"geq"}
keyword = {keyword_let | keyword_rule | keyword_end | unary_op}
name = @{!keyword ~ ASCII_ALPHA ~ ASCII_ALPHANUMERIC*}
propname = @{ASCII_ALPHA}
number = @{"-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?}
variable = @{"?" ~ name}
unary = {unary_op ~ expression}
binary = {simple_expression ~ binary_op ~ expression}
simple_expression = {number | variable | unary}
expression = { binary | simple_expression }

assignment = {keyword_let ~ name ~ number }
property = {propname ~ expression}
invocation = {name ~ (property)* }

rule = {keyword_rule ~ name ~ expression? ~ invocation* ~ keyword_end?}
statement = {rule | assignment | invocation}

